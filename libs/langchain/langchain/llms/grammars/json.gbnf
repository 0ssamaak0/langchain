root   ::= object EOF
value  ::= object | array | string | number | ("true" | "false" | "null") hws

object ::=
  "{" hws (
            string ":" hws value
    ("," hws string ":" hws value)*
  )? "}" hws

array  ::=
  "[" hws (
            value
    ("," hws value)*
  )? "]" hws

string ::=
  "\"" (
    [^"\\] |
    "\\" (["\\/bfnrt] | "u" [0-9a-fA-F] [0-9a-fA-F] [0-9a-fA-F] [0-9a-fA-F]) # escapes
  )* "\"" hws

number ::= ("-"? ([0-9] | [1-9] [0-9]*)) ("." [0-9]+)? ([eE] [-+]? [0-9]+)? hws

# Horizontal whitespace
hws ::= [ \t]*

# EOF as a unique stop token after the JSON structure.
EOF ::= "\n"